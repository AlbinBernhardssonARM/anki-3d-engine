// Copyright (C) 2009-2020, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

// if VARIANT==0 then the checkerboard pattern is (render on 'v'):
// -----
// |v| |
// | |v|
// -----

#pragma anki mutator VARIANT 0 1

#pragma anki start comp
#include <shaders/Functions.glsl>
#include <shaders/Pack.glsl>
#include <shaders/glsl_cpp_common/Ssr.h>
#include <shaders/Tonemapping.glsl>
#include <shaders/SsRaymarching.glsl>

const UVec2 WORKGROUP_SIZE = UVec2(16, 16);
layout(local_size_x = WORKGROUP_SIZE.x, local_size_y = WORKGROUP_SIZE.y, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform sampler u_trilinearClampSampler;
layout(set = 0, binding = 1) uniform sampler u_nearestNearestClampSampler;
layout(set = 0, binding = 2) uniform texture2D u_gbufferRt1;
layout(set = 0, binding = 3) uniform texture2D u_gbufferRt2;
layout(set = 0, binding = 4) uniform texture2D u_depthRt;
layout(set = 0, binding = 5) uniform texture2D u_lightBufferRt;

layout(set = 0, binding = 6, rgba16f) uniform image2D out_img;

layout(set = 0, binding = 7, row_major) uniform u_
{
	SsrUniforms u_unis;
};

void main()
{
	// Compute a global invocation ID that takes the checkerboard pattern into account
	IVec2 fixedInvocationId = IVec2(gl_GlobalInvocationID.xy);
	fixedInvocationId.x *= 2;
#if VARIANT == 0
	fixedInvocationId.x += ((fixedInvocationId.y + 1) & 1);
#else
	fixedInvocationId.x += ((fixedInvocationId.y + 0) & 1);
#endif

	if(fixedInvocationId.x >= I32(u_unis.m_framebufferSize.x) || fixedInvocationId.y >= I32(u_unis.m_framebufferSize.y))
	{
		// Skip threads outside the writable image
		return;
	}

	const Vec2 uv = (Vec2(fixedInvocationId.xy) + 0.5) / Vec2(u_unis.m_framebufferSize);

	// Read part of the G-buffer
	const F32 roughness = readRoughnessFromGBuffer(u_gbufferRt1, u_trilinearClampSampler, uv);
	const Vec3 worldNormal = readNormalFromGBuffer(u_gbufferRt2, u_trilinearClampSampler, uv);

	// Get depth
	const F32 depth = textureLod(u_depthRt, u_trilinearClampSampler, uv, 0.0).r;

	// Get view pos
	const Vec4 viewPos4 = u_unis.m_invProjMat * Vec4(UV_TO_NDC(uv), depth, 1.0);
	const Vec3 viewPos = viewPos4.xyz / viewPos4.w;

	// Compute refl vector
	const Vec3 viewDir = normalize(viewPos);
	const Vec3 viewNormal = u_unis.m_normalMat * worldNormal;
	const Vec3 reflVec = reflect(viewDir, viewNormal);

	// Rand idx
	U32 randIdx = (fixedInvocationId.x & 1) * 2 + (fixedInvocationId.y & 1);
	randIdx = (randIdx + u_unis.m_frameCount & 3) & 3;

	// Do the heavy work
	Vec3 hitPoint;
	F32 hitAttenuation;
#if 0
	raymarch(viewPos,
		reflVec,
		0.2,
		uv,
		depth,
		u_unis.m_projMat,
		randIdx,
		u_unis.m_maxSteps,
		u_depthRt,
		u_nearestNearestClampSampler,
		u_unis.m_depthMipCount,
		u_unis.m_depthBufferSize,
		hitPoint,
		hitAttenuation);
#else
	const U32 lod = 1;
	raymarchGroundTruth(viewPos,
		reflVec,
		uv,
		depth,
		u_unis.m_projMat,
		randIdx,
		u_unis.m_maxSteps,
		u_depthRt,
		u_nearestNearestClampSampler,
		F32(lod),
		u_unis.m_depthBufferSize >> lod,
		16u,
		hitPoint,
		hitAttenuation);
#endif

	// Read the reflection
	Vec4 outColor;
	ANKI_BRANCH if(hitAttenuation > 0.0)
	{
		// Read the refl

		// Reproject the UV because you are reading the previous frame
		const Vec4 v4 = u_unis.m_prevViewProjMatMulInvViewProjMat * Vec4(UV_TO_NDC(hitPoint.xy), hitPoint.z, 1.0);
		hitPoint.xy = NDC_TO_UV(v4.xy / v4.w);

		// Compute the LOD based on the roughness
		const F32 lod = F32(u_unis.m_lightBufferMipCount - 1u) * roughness;

		// Read the light buffer
		outColor.rgb = textureLod(u_lightBufferRt, u_trilinearClampSampler, hitPoint.xy, lod).rgb;
		outColor.rgb = clamp(outColor.rgb, 0.0, FLT_MAX); // Fix the value just in case
		outColor.rgb *= hitAttenuation;
		outColor.a = 1.0 - hitAttenuation;
	}
	else
	{
		outColor = Vec4(0.0, 0.0, 0.0, 1.0);
	}

	// Store
	//const Vec3 arr[4] = Vec3[](Vec3(1, 0, 0), Vec3(0, 1, 0), Vec3(0, 0, 1), Vec3(1, 0, 1));
	//outColor = Vec4(arr[randIdx], 1);
	imageStore(out_img, fixedInvocationId, outColor);
}
#pragma anki end
